plugins {
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
    id 'org.springframework.boot' version '3.0.5'
    id 'com.google.cloud.tools.jib' version '3.3.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group = 'org.springframework.samples'
sourceCompatibility = '17'

ext {
    webjarsBootstrapVersion = '3.3.6'
    webjarsJqueryUiVersion = '1.11.4'
    webjarsJqueryVersion = '2.2.4'

    jacocoVersion = '0.8.11'
}


jib {
    container {
        creationTime = "USE_CURRENT_TIMESTAMP"
        mainClass = 'org.springframework.samples.petclinic.PetClinicApplication'
        user = '1000'
    }

    from {
        image = "gcr.io/distroless/java17-debian12"
        platforms {
            platform {
                architecture = "amd64"
                os = "linux"
            }
        }
    }
    extraDirectories {
        permissions = [
                '/app': '755',
        ]
    }
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

test {
    finalizedBy 'jacocoTestReport'
    testLogging.exceptionFormat = 'full'
    // 테스트 케이스를 넣으세요
}

//최소 커버리지 설정
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
}


repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:mysql:1.17.6'

    runtimeOnly 'com.mysql:mysql-connector-j:8.0.33'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'com.github.ben-manes.caffeine:caffeine'

    implementation "org.webjars:jquery:${webjarsJqueryVersion}"
    implementation "org.webjars:jquery-ui:${webjarsJqueryUiVersion}"
    implementation "org.webjars:bootstrap:${webjarsBootstrapVersion}"
}